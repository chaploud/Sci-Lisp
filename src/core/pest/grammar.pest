COMMENT = _{ ";" ~ (!NEWLINE ~ ANY)* ~ NEWLINE }
ws = _{ " " | "\t" | "\r" | "\n" | "," }

prefix_char = _{ "#" | "'" | "`" | "~" | "~@" | "@" | "\\" | ":" }
special_char = _{ "!" | "$" | "%" | "&" | "-" | "=" | "^" | "|" | "+" | "*" | "<" | ">" | "?" | "_" | "/"}
separation_char = _{ "." }

identifier = _{ ( ASCII_ALPHA | special_char) ~ (ASCII_ALPHANUMERIC | special_char)* }

// atom
nil = @{ "nil" }
bool = @{ "true" | "false" }
f64 = @{ int ~ "." ~ int? ~ exp?
       | int ~ exp                                  // -0_2e+02
       | pm? ~ "." ~ digit ~ (digit | "_")* ~ exp?  // -.0_0e5
       | nan
       | inf }
i64 = @{ int ~ !("." | ^"e") }
  int = _{ pm? ~ digit ~ (digit | "_")* }
  digit = _{ ASCII_DIGIT }
  pm = _{ ("-" | "+") }
  exp = _{ ^"e" ~ i64 }
  nan = _{ "nan" }
  inf = _{ pm? ~ "inf" }
symbol = @{ identifier }
keyword = @{ ":" ~ identifier}

// string
regex = { "#\"" ~ string_inner ~ "\"" }
string = { "\"" ~ string_inner ~ "\"" }
  string_inner = { char* }
  char = _{ !("\"" | "\\") ~ ANY | "\\" ~ ANY }

// collection
list = { "(" ~ collection_inner ~ ")" | empty_list }
vector = { "[" ~ collection_inner ~ "]" | empty_vector }
map = { "{" ~ collection_inner ~ "}" | empty_map }
set = { "#{" ~ collection_inner ~ "}" | empty_set }
  collection_inner = _{ expr ~ (ws+ ~ expr)* }
  empty_list = _{ "(" ~ ws* ~ ")" }
  empty_vector = _{ "[" ~ ws* ~ "]" }
  empty_map = _{ "{" ~ ws* ~ "}" }
  empty_set = _{ "#{" ~ ws* ~ "}" }

// quotes
quote = { "'" ~ expr }
syntax_quote = { "`" ~ expr }
unquote = { "~" ~ expr }
unquote_splicing = { "~@" ~ expr }

expr = _{ nil | bool | i64 | f64 | symbol | keyword | regex | string
        | list | vector | map | set
        | quote | syntax_quote | unquote | unquote_splicing }

// top level
scilisp = { SOI ~ scilisp_inner ~ EOI}
  scilisp_inner = _{ expr ~ (ws+ ~ expr)* }
  empty_scilisp = _{ SOI ~ ws* ~ EOI }

// TODO:
// (symbol.member) dot
// ([0:2] expr)([-1] expr) slice / at
